#define max_process_num		64
#define err_unknown			0x100
#define err_no_permission	0x101
set push, a
set a, free_space_start
shr a, 9
add a, 1
set [page_header_start + 127], -1
jsr fn_request_memory
set a, pop
set pc, pop
:fn_request_memory
set push, b
set push, c
iaq 1
set b, page_header_start
set c, 0
:.loop_a
ife [b], 0
add c, 1
ifn [b], 0
set c, 0
ife c, a
set pc, .found_space
ife b, page_header_start + 127
set pc, .no_space
add b, 1
set pc, .loop_a
:.found_space
set c, b
sub c, a
add c, 1
set [b], -1
:.loop_b
ife b, c
set pc, .do_header
set [b + -1], b
sub [b + -1], page_header_start
sub b, 1
set pc, .loop_b
:.do_header
set a, b
sub a, page_header_start
set b, [current_process]
add b, process_first_pages_start
:.loop_c
ife [b], -1
set pc, .found_end
set b, [b]
add b, page_header_start
set pc, .loop_c
:.found_end
set [b], a
add pc, 1
:.no_space
set a, -1
iaq 0
set c, pop
set b, pop
set pc, pop
:fn_request_page
iaq 1
set a, page_header_start
:.loop_a
ife [a], 0
set pc, .found_space
ife a, page_header_start + 127
set pc, .no_space
add a, 1
set pc, .loop_a
:.no_space
set a, -1
iaq 0
set pc, pop
:.found_space
set push, b
set [a], -1
sub a, page_header_start
set b, [current_process]
add b, process_first_pages_start
:.loop_b
ife [b], -1
set pc, .found_end
set b, [b]
add b, page_header_start
set pc, .loop_b
:.found_end
set [b], a
iaq 0
set b, pop
set pc, pop
:fn_clear_process
set push, b
iaq 1
set a, [a + process_first_pages_start]
:.loop_a
add a, page_header_start
ife [a], -1
set pc, .done
set b, [a]
set [a], 0
set a, b
set pc, .loop_a
:.done
set [a], 0
iaq 0
set b, pop
set pc, pop
:fn_free_page
set push, b
iaq 1
set b, [current_process]
shl b, 5
set b, [b + process_first_pages_start]
:.loop_a
add b, page_header_start
ife [b], a
set pc, .found_it
ife [b], -1
set pc, .not_owned
set b, [b]
set pc, .loop_a
:.found_it
add a, page_header_start
set [b], [a]
set [a], 0
set a, 0
iaq 0
set b, pop
set pc, pop
:.not_owned
set a, err_no_permission
iaq 0
set b, pop
set pc, pop
:fn_clear_page
set push, i
set push, j
iaq 1
shl a, 9
set i, a
add a, 512
:.loop_a
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
sti [i], 0
ifn i, a
set pc, .loop_a
iaq 0
set j, pop
set i, pop
set pc, pop
:fn_copy_page
set push, i
set push, j
iaq 1
shl a, 9
shl b, 9
set i, a
set j, b
add a, 512
:.loop_a
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
sti [i], [j]
ifn i, a
set pc, .loop_a
iaq 0
set j, pop
set i, pop
set pc, pop
:page_header_start
#reserve 128
:process_first_pages_start
#reserve 64
:current_process
dat 0x0000